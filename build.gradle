plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
    id 'com.diffplug.spotless' version '6.12.0'
    id 'com.github.ben-manes.versions' version '0.44.0'
}

group = 'com.formkiq'
version = '1.13.0'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
    mavenLocal()
    mavenCentral()
}

spotless { 
    java { 
      eclipse().configFile project.rootProject.file("spotless.eclipseformat.xml") 
    } 
}
  
dependencies {
    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
    implementation "software.amazon.awssdk:auth:2.20.120"
    
    implementation "javax.ws.rs:javax.ws.rs-api:2.1.1"
    implementation "com.squareup.okhttp3:okhttp:4.10.0"
    implementation "com.squareup.okhttp3:logging-interceptor:4.10.0"
    implementation "com.google.code.gson:gson:2.9.1"
    implementation "io.gsonfire:gson-fire:1.8.5"
    implementation "org.apache.commons:commons-lang3:3.12.0"
    compileOnly "jakarta.annotation:jakarta.annotation-api:1.3.5"
    implementation "org.openapitools:jackson-databind-nullable:0.2.6"

    // test dependencies
    testImplementation "org.junit.jupiter:junit-jupiter-engine:5.9.1"
    testImplementation "org.junit.platform:junit-platform-runner:1.9.1"
    testImplementation "org.mockito:mockito-core:3.12.4"
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'client'
            from components.java
            
            pom {
                name = 'FormKiQ Client'
                description = 'The Official Client library for FormKiQ'
                url = 'https://github.com/formkiq/formkiq-java-client'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'mfriesen'
                        name = 'Mike Friesen'
                        email = 'mike@formkiq.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/formkiq/formkiq-java-client.git'
                    developerConnection = 'scm:git:ssh://github.com/formkiq/formkiq-java-client.git'
                    url = 'https://github.com/formkiq/formkiq-java-client.git'
                }
            }
        }
    }
    repositories {
        maven {
          credentials {
            username project.repoUser
            password project.repoPassword
          }
          //url "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
          url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
    if(JavaVersion.current().isJava8Compatible()) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

check {
      dependsOn(tasks.publishToMavenLocal)
}

artifacts {
    archives jar
    
    archives javadocJar
    archives sourcesJar
}

signing {
    sign publishing.publications.mavenJava
}

spotlessJavaCheck.dependsOn spotlessJavaApply
